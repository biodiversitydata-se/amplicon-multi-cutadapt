import os

# this container defines the underlying OS for each job when using the workflow
# with --use-conda --use-singularity
singularity: "docker://continuumio/miniconda3:4.9.2"

# Set default config file
configfile: "config/config.yaml"

localrules: concat_samples, all, download_bold, filter_bold

def read_samples(sample_list):
    import pandas as pd
    df = pd.read_csv(sample_list, sep="\t", index_col=0, header=None)
    return df.to_dict()[1]

def fastq_files(data_dir, sample, i):
    # Open the .lst file for the sample
    with open("{}/{}.lst".format(data_dir, sample), 'r') as fh:
        files = []
        for line in fh:
            if f"_R{i}_" in line:
                files.append("{}/{}".format(data_dir, line.rstrip()))
    return " ".join(sorted(files))

# Read samples from file
samples = read_samples(config["sample_list"])

rule all:
    input:
        expand("results/cutadapt/{sample}_R{i}.cutadapt.fastq.gz", 
            sample = samples.keys(), i = [1,2]),
        "resources/bold/bold.info.tsv"

rule download_bold:
    output:
        "resources/bold/bold.fasta"
    params:
        tmpdir = "$TMPDIR/bold",
        taxa = " ".join(config["taxa"]),
        outdir = lambda wildcards, output: os.path.dirname(output[0])
    log: "logs/resources/bold_dl.log"
    shell:
        """
        python src/bold_dl.py --taxa {params.taxa} -o {params.tmpdir} > {log} 2>&1
        cat {params.tmpdir}/*.fasta > {output[0]}
        """

rule download_bold_data:
    "Downloads information for sequences contained in bold fasta file"
    input:
        "resources/bold/bold.fasta"
    output:
        "resources/bold/bold.info.tsv"
    params:
        num_recs = 100
    shell:
        """
        python src/bold_taxonomy.py {input[0]} \
            --outfile {output[0]} --num_recs {params.num_recs}
        """

rule concat_samples:
    """This rule concatenates sample fastq files from multiple lanes"""
    output:
        "data/{sample}_R{i}.fastq.gz"
    params:
        tmp="$TMPDIR/{sample}_R{i}.fastq.gz",
        files=lambda wildcards: fastq_files(config["paths"]["raw_data_dir"], wildcards.sample, wildcards.i),
    shell:
        """
        cat {params.files} > {params.tmp}
        mv {params.tmp} {output[0]}
        """

rule cutadapt:
    input:
        "data/{sample}_R1.fastq.gz",
        "data/{sample}_R2.fastq.gz"
    output:
        "results/cutadapt/{sample}_R1.cutadapt.fastq.gz",
        "results/cutadapt/{sample}_R2.cutadapt.fastq.gz"
    log: "logs/cutadapt/{sample}.log"
    threads: config["cutadapt"]["threads"]
    params:
        f=" ".join(["-a {}".format(x) for x in config["primers"]["forward"]]),
        r=" ".join(["-A {}".format(x) for x in config["primers"]["reverse"]])
    resources:
        runtime = lambda wildcards, attempt: attempt**2*30
    shell:
        """
        cutadapt -j {threads} {params.f} {params.r} -o {output[0]} -p {output[1]} {input[0]} {input[1]} > {log} 2>&1
        """
